#INCLUDE "Protheus.ch"
#INCLUDE "TOTVS.ch"
#INCLUDE "TopConn.ch"

/*/{Protheus.doc} User Function Projeto02
    Segundo projeto para inclusão dos registros.
    @type  Function
    @author Filipe Souza
    @since 01/12/2022
/*/
User Function Projeto02()

    Private oDlg1
    Private cTituloJanela := "PROJETO 02 - ADVPL Protheus"    

    //declarar variáveis do Folder
    Private oFolder
    Private aFolder

    //variáveis reverentes aos objetos do Folder1(Dados Pessoais)
    Private oSayCod, oGetCod, cGetCod, oSayNome, oGetNome, cGetNome, oSayNReduz,;
            oGetNReduz, cGetNReduz, oSayDTNasc, oGetDTNasc, dGetDTNasc, oSayDDD, oGetDDD,;
            cGetDDD, oSayTel, oGetTel, cGetTel, oSayCpf, oGetCpf, cGetCpf, oGroupSexo
    Private aSexo :={"Masculino","Feminino"}
    Private nSexo := 1
    Private oRadioSexo
    Private cSexo :=""

    //variáveis reverentes aos objetos do Folder2(Endereço)
    Private oSayCep, oGetCep, cGetCep, oSayEnd, oGetEnd, cGetEnd, oSayNum, oGetNum,;
            cNum, oSayCompl, oGetCompl, cCompl, cGetCompl, oSayImovel, oComboImovel,;
            oSayBairro, oGetBairro, cBairro, oSayCidade, oGetCidade, cGetCidade, oSayUf, oComboUf
    Private aImovel := {"Casa","Apart.","Outro"}
    Private cImovel := aImovel[1]
    Private aUf := {"RO","AC","AM","RR","PA","AP","TO","MA","PI","CE","RN","PB","PE","AL","SE",;
                    "BA","MG","ES","RJ","SP","PR","SC","RS","MS","MT","GO","DF"}
    Private cGetUf := aUf[1]
    
    //variáveis reverentes aos objetos do Folder3(outros)
    Private aEstCivil := {"Solteiro(a)","Namorando","Noivo(a)","Casado(a)","União Estável","Divorciado(a)","Viúvo(a)"}
    Private cEstCivil := aEstCivil[1]
    Private aPet :={"Sim","Não"}
    Private nPet := 1
    Private lCkAdvpl := .F.
    Private lCkSql   := .F.
    Private lCkJava  := .F.
    Private lCkJS    := .F.
    Private cUrl := "C:\TOTVS12133\Protheus\protheus_data\system\"
    Private cImgAdd := cUrl+"Add.png"
    Private cImgAlt := cUrl+"Alt.png"
    Private cImgPesq:= cUrl+"Pesq.png"
    Private cImgDel := cUrl+"Del.png"
    Private cDados := ""
    Private aStatus := {"Sim","Não"}
    Private nSt := 1
    Private oSayEstCivil, oComboEstCivil, oGroupPet, oRadiopet,oGroupSt,  oRadioSt, oGroupLing, oCkAdvpl,;
            oCkSql, oCkJava, oCkJS, oSayObs, cGetObs , oTMultiget, oBtMarkAll,;
            oBtUnMarkAll, oBtDados, oBtEnd, oBtoutros
    Private oBtSave,oBtIn,oBtCan,oBtDel
    
    
    //Inicio da construção da Tela
    oDlg1   := TDialog():New(0,0,720,1120,cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

    aFolder := {"Dados Pessoais","Endereço","Outros"}
    oFolder := TFolder():New(0,0,aFolder,aFolder,,,,,.T.,,600,120)
    //insere um TGet e uma TSay em cada aba da folder
    //elementos visuais que compoõem o Folder/Pasta1 Dados Pessoais
    oSayCod   := TSay():New(05,05,{||'Codigo'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCod   := Space(6)                                                 //exibe na aba 1
    oGetCod   :=TGet():New(15,05,{|u|IIF(Pcount()>0,cGetCod:=u,cGetCod)},oFolder:aDialogs[1],040,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.T.,.F.,,cGetCod,,,,)
    oGetCod:Disable()

    oSayNome  := TSay():New(05,60,{||'Nome'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetNome  := Space(50)                                                 //exibe na aba 1
    oGetNome  :=TGet():New(15,60,{|u|IIF(Pcount()>0,cGetNome:=u,cGetNome)},oFolder:aDialogs[1],096,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,,)
    oGetNome:cf3:='ZZ1'
    
    oSayNReduz:= TSay():New(05,170,{||'Nome Reduzido'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetNReduz:= Space(30)                                                 //exibe na aba 1
    oGetNReduz:=TGet():New(15,170,{|u|IIF(Pcount()>0,cGetNReduz:=u,cGetNReduz)},oFolder:aDialogs[1],070,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNReduz,,,,)
    
    oSayDTNasc:= TSay():New(05,250,{||'Data de Nascimento'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    dGetDTNasc:= DATE()                                                 //exibe na aba 1
    oGetDTNasc:= TGet():New(15,250,{|u|IIF(Pcount()>0,dGetDTNasc:=u,dGetDTNasc)},oFolder:aDialogs[1],050,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,/**/,,,,.T.)//ultumo .T. mostra icone de calendario
    
    oSayCpf   := TSay():New(35,05,{||'Cpf'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCpf   := Space(11)                                                 //exibe na aba 1
    oGetCpf   := TGet():New(45,05,{|u|IIF(Pcount()>0,cGetCpf:=u,cGetCpf)},oFolder:aDialogs[1],050,009,"@E 99999999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCpf,,,,)
    oGetCpf:cf3:='ZZ1CPF'

    oSayDDD   := TSay():New(35,70,{||'DDD'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetDDD   := Space(2)                                                 //exibe na aba 1
    oGetDDD   := TGet():New(45,70,{|u|IIF(Pcount()>0,cGetDDD:=u,cGetDDD)},oFolder:aDialogs[1],020,009,"@E 99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDD,,,,)
    
    oSayTel   := TSay():New(35,100,{||'Telefone'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetTel   := Space(10)                                                 //exibe na aba 1
    oGetTel   := TGet():New(45,100,{|u|IIF(Pcount()>0,cGetTel:=u,cGetTel)},oFolder:aDialogs[1],40,009,"@E 99999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetTel,,,,)
    
    oGroupSexo:= tGroup():New(35,150,65,200,"Sexo",oFolder:aDialogs[1],,,.T.)
    oRadioSexo:= TRadMenu():New(45,155,aSexo,{|u|IIF(Pcount()>0,nSexo:=u,nSexo)},oFolder:aDialogs[1],,,,,,,,40,20,,,,.T.)

    oSayEstCivil  := TSay():New(35,210,{||'Estado Civil'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,35,15)
    oComboEstCivil:= TComboBox():New(45,215,{|u|IIF(Pcount()>0,cEstCivil:=u,cEstCivil)},aEstCivil,50,15,oFolder:aDialogs[1],,,,,,.T.,,,,,,,,,'cEstCivil')
    
    //elementos visuais que compoõem o Folder/Pasta2 Endereço
    oSayCep   := TSay():New(05,05,{||'Cep'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,30,20)
    cGetCep      := Space(10)                                                 //exibe na aba 2              //FWAlertSuccess('','')
    oGetCep   := TGet():New(15,05,{|u|IIF(Pcount()>0,cGetCep:=u,cGetCep)},oFolder:aDialogs[2],20,009,"@E 99999-999",,0,,,.F.,,.T.,,.F.,/*17*/,.F.,.F.,,.F.,.F.,,cGetCep,,,,)
    oGetCep:bValid    := {|| U_xCep(cGetCep)}
    
    oSayEnd   := TSay():New(05,50,{||'Endereço'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetEnd   := Space(80)                                                 //exibe na aba 2
    oGetEnd   := TGet():New(15,50,{|u|IIF(Pcount()>0,cGetEnd:=u,cGetEnd)},oFolder:aDialogs[2],130,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEnd,,,,)
    
    oSayNum   := TSay():New(05,190,{||'Número'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,100,20)
    cGetNum   := Space(5)                                                 //exibe na aba 2
    oGetNum   := TGet():New(15,190,{|u|IIF(Pcount()>0,cGetNum:=u,cGetNum)},oFolder:aDialogs[2],30,009,"@E 99999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNum,,,,)
    
    oSayImovel   := TSay():New(05,230,{||'Tipo de Imóvel'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    oComboImovel := TComboBox():New(15,230,{|u|IIF(Pcount()>0,cImovel:=u,cImovel)},aImovel,60,15,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')
    
    oSayCompl   := TSay():New(05,300,{||'Complemento'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCompl   := Space(20)                                                 //exibe na aba 2
    oGetCompl   := TGet():New(15,300,{|u|IIF(Pcount()>0,cGetCompl:=u,cGetCompl)},oFolder:aDialogs[2],130,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCompl,,,,)
    
    oSayUf       := TSay():New(35,05,{||'Estado'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,30,15)
    oComboUf     := TComboBox():New(45,05,{|u|IIF(Pcount()>0,cGetUf:=u,cGetUf)},aUf,30,15,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cGetUf')
    
    oSayCidade   := TSay():New(35,50,{||'Cidade'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,100,20)
    cGetCidade   := Space(30)                                                 //exibe na aba 2
    oGetCidade   := TGet():New(45,50,{|u|IIF(Pcount()>0,cGetCidade:=u,cGetCidade)},oFolder:aDialogs[2],100,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCidade,,,,)
    

    oSayBairro   := TSay():New(35,190,{||'Bairro'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetBairro   := Space(60)                                                 //exibe na aba 2
    oGetBairro   := TGet():New(45,190,{|u|IIF(Pcount()>0,cGetBairro:=u,cGetBairro)},oFolder:aDialogs[2],100,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBairro,,,,)    

    //elementos visuais que compoõem o Folder/Pasta3 outros    
    oGroupPet     := tGroup():New(05,05,40,50,"Possui Pet?",oFolder:aDialogs[3],,,.T.)
    oRadiopet     := TRadMenu():New(15,10,aPet,{|u|IIF(Pcount()>0,nPet:=u,nPet)},oFolder:aDialogs[3],,,,,,,,30,20,,,,.T.)

    oGroupLing    := tGroup():New(05,55,60,140,"Linguagens que domina:",oFolder:aDialogs[3],,,.T.)
                                                                                            // se clicar no check a variável recebe o valor inverso dela     
    oCkAdvpl      := TCheckBox():New(15,60,'Advpl',{||lCkAdvpl},oFolder:aDialogs[3],50,210,,{||lCkAdvpl:= !lCkAdvpl},,,,,,.T.,,,)
    oCkSql        := TCheckBox():New(25,60,'Sql',{||lCkSql},    oFolder:aDialogs[3],50,210,,{||lCkSql  := !lCkSql},,,,,,.T.,,,)
    oCkJava       := TCheckBox():New(35,60,'Java',{||lCkJava},  oFolder:aDialogs[3],50,210,,{||lCkJava := !lCkJava},,,,,,.T.,,,)
    oCkJS         := TCheckBox():New(45,60,'JScript',{||lCkJS}, oFolder:aDialogs[3],50,210,,{||lCkJS   := !lCkJS},,,,,,.T.,,,)
    oBtMarkAll    := TButton():New(20,90,"Todas", oFolder:aDialogs[3],;
                    {||lCkAdvpl:=.T., lCkSql:=.T., lCkJava:=.T.,lCkJS:=.T.},30,10,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtUnMarkAll  := TButton():New(35,90,"Nenhuma", oFolder:aDialogs[3],;
                    {||lCkAdvpl:=.F., lCkSql:=.F., lCkJava:=.F.,lCkJS:=.F.},30,10,,,.F.,.T.,.F.,,.F.,,,.F.)
    
    oSayObs       := TSay():New(05,150,{||'Observação'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLACK,CLR_WHITE,50,15)
    oTMultiget    := tMultiget():new( 15, 150,{|u|IIF(Pcount()>0,cGetObs:=u,cGetObs)},oFolder:aDialogs[3], 150, 50, , , , , , .T. )
    
    oGroupSt     := tGroup():New(05,305,40,350,"Ativo(a)?",oFolder:aDialogs[3],,,.T.)
    oRadioSt     := TRadMenu():New(15,310,aStatus,{|u|IIF(Pcount()>0,nSt:=u,nSt)},oFolder:aDialogs[3],,,,,,,,30,20,,,,.T.)

    //iniciar campos desabilitados, para criar novo ou pesquisar
    //oGetNome:Disable()
    oGetNReduz:Disable()
    oGetDTNasc:Disable()
    //oGetCpf:Disable()
    oGetDDD:Disable()
    oGetTel:Disable()
    oComboEstCivil:Disable()
    oGetCep:Disable()
    oGetEnd:Disable()
    oGetNum:Disable()
    oComboImovel:Disable()
    oGetCompl:Disable()
    oComboUf:Disable()
    oGetCidade:Disable()
    oGetBairro:Disable()
    oTMultiget:Disable()
    
    // linha, coluna1 2 3 4, width, height, dialog
    oTBitAdd := TBitmap():New(120, 22,  75, 66, NIL, cImgAdd,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitAlt := TBitmap():New(120, 82,  75, 66, NIL, cImgAlt,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitCons:= TBitmap():New(120, 145, 75, 66, NIL, cImgPesq,.T.,oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitDel := TBitmap():New(120, 202, 75, 66, NIL, cImgDel,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)

    //https://siga0984.wordpress.com/2014/12/03/classes-em-advpl-parte-03/
    @ 150,20 BUTTON oBtIn PROMPT 'Novo' ;
        ACTION ( U_xNovo()) SIZE 40, 15 OF oDlg1 PIXEL    
    oBtSave := APBUTTON():NEW(oDlg1, "Salvar", 150, 20, 40, 15, {|| oBtIn:Show(),oBtSave:Hide(),oBtCan:Hide(),U_xSalvarZZ1() })
    oBtCan  := APBUTTON():NEW(oDlg1, "Cancelar", 165, 20, 40, 10, {|| U_xCancel(),rollbackSx8() })

    //oBtIn   := TButton():New(150,20,"Incluir" ,oDlg1,{|| U_IncluirZZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtAlt  := TButton():New(150,80, "Alterar"  ,,{|| U_xUpdateZZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtCons := TButton():New(150,140,"Consultar",,{|| U_xConsultar() },40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtDel  := TButton():New(150,200,"Excluir"  ,,{|| U_xDeleteZZ1() },40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtAlt:Disable()
    oBtDel:Disable()
    oDlg1:ACTIVATE(,,,.T.)

Return

User Function xNovo()
    cGetCod:=GETSXENUM("ZZ1","ZZ1_COD") 
    oBtSave:Show()
    oBtCan:Show()
    oBtIn:Hide()
    //oGetNome:Enable()
    oGetNReduz:Enable()
    oGetDTNasc:Enable()
    oGetCpf:Enable()
    oGetDDD:Enable()
    oGetTel:Enable()
    oComboEstCivil:Enable()
    oGetCep:Enable()
    oGetEnd:Enable()
    oGetNum:Enable()
    oComboImovel:Enable()
    oGetCompl:Enable()
    oComboUf:Enable()
    oGetCidade:Enable()
    oGetBairro:Enable()
    oTMultiget:Enable()
Return

User Function xSalvarZZ1()
    Local lRet  := .F.
    DbSelectArea("ZZ1")
    DbSetOrder(4)
    If DBSeek(FwXFilial("ZZ1")+cGetCpf)
        FwAlertError('Cliente com CPF:'+ cGetCpf +' já cadastrado, verifique o documento.')
        ZZ1->(DbCloseArea())
    else
        DbSetOrder(1)
        if(RecLock("ZZ1", .T.))
            ZZ1->ZZ1_COD    := cGetCod     //U_xNextCod() 
            ZZ1->ZZ1_FILIAL := FwXFilial("ZZ1")
            ZZ1->ZZ1_NOME   :=cGetNome 
            ZZ1->ZZ1_NOMERE :=cGetNReduz
            ZZ1->ZZ1_DTNASC :=dGetDTNasc
            ZZ1->ZZ1_DDD    :=cGetDDD
            ZZ1->ZZ1_TEL    :=StrTran( cGetTel ,'-','') 
            ZZ1->ZZ1_SEXO   :=CVALTOCHAR(nSexo)
            ZZ1->ZZ1_CPF    :=cGetCpf
            ZZ1->ZZ1_CEP    :=StrTran(cGetCep,'-','') 
            ZZ1->ZZ1_END    :=cGetEnd 
            ZZ1->ZZ1_NUM    :=cGetNum 
            ZZ1->ZZ1_COMPL  :=cGetCompl 
            ZZ1->ZZ1_BAIRRO :=cGetBairro 
            ZZ1->ZZ1_CIDADE :=cGetCidade 
            ZZ1->ZZ1_UF     :=cGetUf
            ZZ1->ZZ1_TPIMOV :=cImovel
            ZZ1->ZZ1_CIVIL  :=cEstCivil 
            ZZ1->ZZ1_PET    :=CVALTOCHAR(nPet)     
            ZZ1->ZZ1_STATUS :=CVALTOCHAR(nSt)  
            ZZ1->ZZ1_ADVPL  :=IIF(lCkAdvpl,'1','0')
            ZZ1->ZZ1_SQL    :=IIF(lCkSql,'1','0')
            ZZ1->ZZ1_JAVA   :=IIF(lCkJava,'1','0')
            ZZ1->ZZ1_JS     :=IIF(lCkJS,'1','0')
            ZZ1->ZZ1_OBS    :=cGetObs 
            ConfirmSx8()
            MsUnlock()
            ZZ1->(DbCloseArea()) 
            lRet   := .T.
            oBtIn:Show()
            oBtSave:Hide()
            oBtCan:Hide()
            U_xCancel()
            FWAlertSuccess("Registrado com sucesso", "Registro")
        else
            rollbackSx8()
            lRet   := .F.
            FwAlertError("Registro com erro, verifique os dados!","Registro")
        EndIf
    Endif    
    
Return lRet

User Function xUpdateZZ1()
    Local lRet  := .F.
    DbSelectArea("ZZ1")
    DbSetOrder(4)    
    if DBSeek(FwXFilial("ZZ1")+cGetCpf)
        RecLock("ZZ1", .F.)        
        //ZZ1->ZZ1_COD    := cGetCod     //U_xNextCod() 
        //ZZ1->ZZ1_FILIAL := FwXFilial("ZZ1")
        ZZ1->ZZ1_NOME   :=cGetNome 
        ZZ1->ZZ1_NOMERE :=cGetNReduz
        ZZ1->ZZ1_DTNASC :=dGetDTNasc
        ZZ1->ZZ1_DDD    :=cGetDDD
        ZZ1->ZZ1_TEL    :=StrTran( cGetTel ,'-','')  
        ZZ1->ZZ1_SEXO   :=CVALTOCHAR(nSexo)
        //ZZ1->ZZ1_CPF    :=cGetCpf
        ZZ1->ZZ1_CEP    :=StrTran(cGetCep,'-','') 
        ZZ1->ZZ1_END    :=cGetEnd 
        ZZ1->ZZ1_NUM    :=cGetNum
        ZZ1->ZZ1_COMPL  :=cGetCompl
        ZZ1->ZZ1_BAIRRO :=cGetBairro
        ZZ1->ZZ1_CIDADE :=cGetCidade 
        ZZ1->ZZ1_UF     :=cGetUf
        ZZ1->ZZ1_TPIMOV :=cImovel
        ZZ1->ZZ1_CIVIL  :=cEstCivil 
        ZZ1->ZZ1_PET    :=CVALTOCHAR(nPet)     
        ZZ1->ZZ1_STATUS :=CVALTOCHAR(nSt) 
        ZZ1->ZZ1_ADVPL  :=IIF(lCkAdvpl,'1','0')
        ZZ1->ZZ1_SQL    :=IIF(lCkSql,'1','0')
        ZZ1->ZZ1_JAVA   :=IIF(lCkJava,'1','0')
        ZZ1->ZZ1_JS     :=IIF(lCkJS,'1','0')
        ZZ1->ZZ1_OBS    :=cGetObs 
        MsUnlock()
        ZZ1->(DbCloseArea()) 
        lRet   := .T.
        oBtIn:Show()
        oBtSave:Hide()
        oBtCan:Hide()
        U_xCancel()
        FWAlertSuccess("Atualizado com sucesso", "Atualização")       
    Endif    
    
Return lRet

User Function xDeleteZZ1()
    DBSelectArea('ZZ1')
    DbSetOrder(4)    
        if DBSeek(FwXFilial("ZZ1")+cGetCpf)
            IF MsgYesNo("Excluir registro com CPF: "+Alltrim(cGetCpf)+" ?", "Excluir registro")
                RecLock('ZZ1',.F.)
                DBDelete()
                MsUnlock()
                U_xCancel()
                FWAlertSuccess('Registro excluído!','Excluir registro')
            EndIf 
        Endif

    ZZ1->(DbCloseArea())
Return
// Ao consulta não consigo editar campos
User Function xConsultar()
    Local xVar
    Local lChar
    DBSelectArea("ZZ1")
    IF Alltrim(cGetNome)<>""
        DBSetOrder(2)
        xVar := Alltrim(cGetNome)
        lChar:=.T.
    elseIF Alltrim(cGetCpf)<>""
        DBSetOrder(4)
        xVar :=Alltrim(cGetCpf)
        lChar:=.F.
    EndIf    
    If DBSeek(FwXFilial("ZZ1")+xVar)
        cGetCod         :=ZZ1->ZZ1_COD
        //se for lChar preenche cpf,  senão preenche nome
        IIF(lChar,cGetCpf:=ZZ1->ZZ1_CPF,;
                  cGetNome:=ZZ1->ZZ1_NOME)        
        cGetNReduz      :=ZZ1->ZZ1_NOMERE
        dGetDTNasc      :=ZZ1->ZZ1_DTNASC 
        cGetDDD         :=ZZ1->ZZ1_DDD   
        cGetTel         :=SubStr(ZZ1->ZZ1_TEL,1,5)+'-'+SubStr(ZZ1->ZZ1_TEL,6,4)      
        nSexo           :=Val(ZZ1->ZZ1_SEXO)
        cGetCep         :=SubStr(ZZ1->ZZ1_CEP ,1,5)+'-'+SubStr(ZZ1->ZZ1_CEP ,6,3)   
        cGetEnd         :=ZZ1->ZZ1_END
        cGetNum         :=ZZ1->ZZ1_NUM
        cGetCompl       :=ZZ1->ZZ1_COMPL 
        cGetBairro      :=ZZ1->ZZ1_BAIRRO
        cGetCidade      :=ZZ1->ZZ1_CIDADE 
        cGetUf          :=ZZ1->ZZ1_UF    
        cImovel         :=ZZ1->ZZ1_TPIMOV
        cEstCivil       :=ZZ1->ZZ1_CIVIL 
        nPet            :=Val(ZZ1->ZZ1_PET)
        nSt             :=Val(ZZ1->ZZ1_STATUS)
        lCkAdvpl        :=IIF(ZZ1->ZZ1_ADVPL=="1",.T.,.F.)
        lCkSql          :=IIF(ZZ1->ZZ1_SQL  =="1",.T.,.F.)
        lCkJava         :=IIF(ZZ1->ZZ1_JAVA =="1",.T.,.F.)
        lCkJS           :=IIF(ZZ1->ZZ1_JS   =="1",.T.,.F.)
        cGetObs         :=ZZ1->ZZ1_OBS 
        oGetNReduz:Enable()
        oGetDTNasc:Enable()
        oGetCpf:Disable()
        oGetDDD:Enable()
        oGetTel:Enable()
        oComboEstCivil:Enable()
        oGetCep:Enable()
        oGetEnd:Enable()
        oGetNum:Enable()
        oComboImovel:Enable()
        oGetCompl:Enable()
        oComboUf:Enable()
        oGetCidade:Enable()
        oGetBairro:Enable()
        oTMultiget:Enable()  
        oBtSave:Show()
        oBtCan:Show() 
        oBtIn:Hide()   
        oBtAlt:Enable()
        oBtDel:Enable()
    else
        FwAlertError('Erro na consulta','Consulta')   

    EndIf
    ZZ1->(DbCloseArea())
    
Return .T.

User Function xCancel()
    cGetCod     := ""    
    cGetNome    := Space(50)  
    cGetNReduz  :=  Space(30)    
    dGetDTNasc  := Date()
    cGetCpf     := Space(11)    
    cGetDDD     := Space(2)      
    cGetTel     := Space(10) 
    cGetCep     := Space(10) 
    cGetEnd     := Space(80) 
    cGetNum     := Space(5) 
    cGetCompl   := Space(20) 
    cGetCidade  := Space(30) 
    cGetBairro  := Space(60) 
    lCkAdvpl    := .F. 
    lCkSql      := .F.  
    lCkJS       := .F.
    lCkJava     := .F.
    cGetObs     := Space(10) 
    //oGetNome:Disable()
    oGetNReduz:Disable()
    oGetDTNasc:Disable()
    //oGetCpf:Disable()
    oGetDDD:Disable()
    oGetTel:Disable()
    oComboEstCivil:Disable()
    oGetCep:Disable()
    oGetEnd:Disable()
    oGetNum:Disable()
    oComboImovel:Disable()
    oGetCompl:Disable()
    oComboUf:Disable()
    oGetCidade:Disable()
    oGetBairro:Disable()
    oTMultiget:Disable()
    
    oBtIn:Show()
    oBtSave:Hide()
    oBtCan:Hide()   
    oBtAlt:Disable()
    oBtDel:Disable() 
    oDlg1:Refresh()
Return

User Function xShowBitMap(nL,nC1,nC2,nC3,nC4,nW,nH,nD)
    oTBitAdd := TBitmap():New(nL, nC1, nW, nH,  NIL, cImgAdd,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitAlt := TBitmap():New(nL, nC2, nW, nH,  NIL, cImgAlt,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitCons:= TBitmap():New(nL, nC3, nW, nH, NIL,  cImgPesq,.T.,oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oTBitDel := TBitmap():New(nL, nC4, nW, nH, NIL,  cImgDel,.T., oDlg1,, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
Return

User Function xShowBt(nL,nC1,nC2,nC3,nC4,nD) 
    oBtIn   := TButton():New(nL,nC1,"Incluir" ,oDlg1,{|| U_IncluirZZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtAlt  := TButton():New(nL,nC2,"Alterar" ,oDlg1,{|| MsgInfo("Alterar","Alterar") },40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtCons := TButton():New(nL,nC3,"Consultar" ,oDlg1,{|| MsgInfo("Consultar","Consultar") },40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oBtDel  := TButton():New(nL,nC4,"Excluir" ,oDlg1,{|| MsgInfo("Excluir","Excluir") },40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
Return

User Function xCep(c)
    Local lRet          := .T. 
    Local aHeader       := {}  
    Local cHeaderRet    := ''  
    Local cResult       := ''  
    Local oResult       := {}  
    Begin Sequence
        If Empty(AllTrim(c))
            FwAlertError('Informe o CEP','Validação')
            Break
        Endif
        cResult := HTTPQuote('https://brasilapi.com.br/api/cep/v2/'+AllTrim(c), "GET", "", , , aHeader, @cHeaderRet)
            If !("200 OK" $ cHeaderRet )
            FwAlertError('Erro na Consulta: ' + cResult,'Validação')
            Break
        Endif
        If !FWJsonDeserialize( cResult, @oResult )
            FwAlertError('Erro no jSon: ' + cResult,'Validação')
            Break
        Endif
        cGetEnd    := DecodeUTF8(oResult:street)
        if At(',',cGetEnd) >0            
            cGetEnd    := SubStr(cGetEnd, 0, At(',',cGetEnd)-1)//retira sinal ,
        EndIf
        if At('-',cGetEnd) >0
            cGetEnd    := SubStr(cGetEnd, 0, At('-',cGetEnd)-1)//retira sinal -            
        EndIf
        cGetUf     := DecodeUTF8(oResult:state)
        cGetCidade    := DecodeUTF8(oResult:city)
        cGetBairro := DecodeUTF8(oResult:neighborhood)
        
        RECOVER
    End Sequence
Return lRet

// ------------------------------------------------------------
CLASS APBUTTON FROM TBUTTON
    PUBLIC  METHOD New() CONSTRUCTOR
    PUBLIC  METHOD Hide()
    PUBLIC  METHOD Show()
ENDCLASS
// Construtor da classe inicializa construtor do botão
// e já seta todas as propriedades e comportamentos desejados
// ( Troca fonte, seta cor e esconde o botão )
METHOD New(oParent,cCaption,nTop,nLeft,nWidth,nHeight,bAction) CLASS APBUTTON
      :New(nTop,nLeft,cCaption,oParent,bAction,nWidth,nHeight,NIL,NIL,NIL,.T.)
_Super:Hide()
Return self

METHOD Hide() CLASS APBUTTON
//MsgInfo("Escondendo o botão ["+::cCaption+"]")
Return _Super:Hide()

METHOD Show() CLASS APBUTTON
//MsgInfo("Mostrando o botão ["+::cCaption+"]")
Return _Super:Show()


